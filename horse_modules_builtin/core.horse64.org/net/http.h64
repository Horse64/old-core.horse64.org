# Copyright (c) 2020-2021, ellie/@ell1e & Horse64 Team (see AUTHORS.md),
# also see LICENSE.md file.
# SPDX-License-Identifier: BSD-2-Clause


import net from core.horse64.org
import uri from core.horse64.org
import system from core.horse64.org

func request(target, method="GET", insecure=no,
             user_agent="horse64-http/" + system.core_version,
             bytes_result=yes) {
    # Parse options;
    const max_header_size = (4096 * 10)
    if type(target) == "string" or type(target) == "bytes" {
        target = uri.parse(
            target, default_protocol="https", guess_port=yes
        )
    }
    if not has_attr(target, "host") or not has_attr(target, "path") or
            not has_attr(target, "port") or not has_attr(target, "protocol") {
        raise new TypeError(
            "target must be string, or have uri.uri attributes"
        )
    }
    if target.protocol.lower() != "http" and
            target.protocol.lower() != "https" {
        raise new ValueError(
            "target is not http or https uri"
        )
    }
    if target.protocol.lower() == "http" and not insecure {
        raise new ValueError(
            "cannot contact untrusted http target with insecure=false"
        )
    }

    # Assemble headers and connect:
    var headers = {
        "User-Agent" -> user_agent,
        "Host" -> target.host + ":" + target.port.as_str
    }
    var conn = net.connect(
        target.host, target.port,
        encrypt=(target.protocol.lower() == "https")
    )

    # Send off HTTP(S) request and get headers:
    conn.write(
        b"HTTP1/1 GET " +
            given not target.path.starts("/") then (b"/" else b"") +
            target.path.as_bytes + b"\r\n" +
        headers.join(":", "\r\n").as_bytes + b"\r\n" +
        b"\r\n"
    )
    var result_headers = conn.read(
        len=max_header_size, upto=b"\r\n\r\n"
    )
    if not result_headers.ends(b"\r\n\r\n") {
        raise new IOError(
            "invalid server response with no headers, or "
            "headers exceeding " + max_header_size.as_str + " bytes"
        )
    }
}

func get(target, insecure=no, bytes_result=yes) {
    return request(target, method="GET", insecure=insecure)
}