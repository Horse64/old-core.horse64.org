
import io from core.horse64.org
import path from core.horse64.org

func main {
    var orig_cwd = path.get_cwd()

    # Create a temp folder and go inside:
    var p = io.add_tmp_dir(prefix="paths_h64_check")
    print("1/6 created a tmp folder: " + p)
    assert(path.exists(p))
    assert(p.contains("paths_h64_check"))
    path.set_cwd(p)
    var p2 = path.get_cwd()
    print("2/6 changed current working directory: " + p2)
    assert(path.normalize(path.to_abs(p)) == path.normalize(path.to_abs(p2)))

    # Test creating a subfolder:
    print("3/6 preparing to create sub dir")
    var p_sub = path.join([p, "myfolder"])
    assert(not path.exists(p_sub))
    io.add_dir(p_sub)
    print("4/6 sub dir was created")
    assert(path.exists(p_sub))

    # Go back to initial cwd, and remove our temp dir again
    path.set_cwd(orig_cwd)
    io.remove(p, recursive=yes)
    print("5/6 removed all directories again")
    assert(not path.exists(p))
    print("6/6 success!")
}

# expected result value: 0
